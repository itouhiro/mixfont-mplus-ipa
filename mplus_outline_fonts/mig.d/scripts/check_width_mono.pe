#!/usr/bin/env fontforge -script
# check glyph width for monospace font
#
# usage: for F in m*-[12]m*ttf; do echo $F; fontforge -script mig.d/scripts/check_width_mono.pe $F; done | tee width_mono.txt

if ($argc <= 1)
  Print("usage: fontforge -script check_width_mono.pe FOOBAR.ttf")
  Quit(0)
endif

Open($1)
SelectWorthOutputting()
foreach
    u = GlyphInfo('Unicode')

    # U+0000-007F    Basic Latin           基本ラテン文字 （ASCII互換）
    # U+0080-00FF    Latin-1 Supplement    ラテン1補助
    # U+0100-017F    Latin Extended-A      ラテン文字拡張A
    # U+0180-024F    Latin Extended-B      ラテン文字拡張B
    # U+0250-02AF    IPA Extensions        IPA拡張 （国際音声記号）

    if (u <= 0u02af)
        w = GlyphInfo('Width')
        if (w != 500 && DrawsSomething(u))
            Print('Width is not 500: ', Utf8(u),' ', UCodePoint(u))
        endif

    # U+3000-303F    CJK Symbols and Punctuation     CJKの記号及び句読点
    # U+3040-309F    Hiragana                        平仮名
    # U+30A0-30FF    Katakana                        片仮名
    # U+31C0-31EF    CJK Strokes                     CJKの筆画
    # U+31F0-31FF    Katakana Phonetic Extensions    片仮名拡張
    # U+3200-32FF    Enclosed CJK Letters and Months 囲みCJK文字・月
    # U+3300-33FF    CJK Compatibility               CJK互換用文字
    # U+3400-4DBF    CJK Unified Ideographs Extension A      CJK統合漢字拡張A
    # U+4E00-9FFF    CJK Unified Ideographs          CJK統合漢字
    # U+F900-FAFF    CJK Compatibility Ideographs    CJK互換漢字
    # U+FF00-FF60    Halfwidth and Fullwidth Forms   半角・全角形(の全角部分)
    elseif ((u >= 0u3000 && u <= 0u9fff) || (u >= 0uf900 && u <= 0ufaff) || (u >= 0uff00 && u <= 0uff60))
        w = GlyphInfo('Width')
        if (w != 1000 && DrawsSomething(u))
           Print('Width is not 1000: ', Utf8(u),' ', UCodePoint(u))
        endif
    endif
endloop
